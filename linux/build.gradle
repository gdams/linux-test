import net.adoptopenjdk.installer.Architecture
import net.adoptopenjdk.installer.VirtualMachine

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "base"
}

allprojects {
    apply plugin: "base"

    repositories {
        mavenCentral()
    }
}

tasks.wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

ext {
    jdkMajorVersion = getJdkMajorVersion()
    jdkVersion = JDK_VERSION
    pkgMetadata = [
        architecture: getJdkArchitecture(),
        vm          : getJdkVirtualMachine(),
        description : "${jdkDistributionType.description} ${jdkMajorVersion} (${jdkDistributionType.name()}) with ${getJdkVirtualMachine().description()} by ${getVendor()}",
        homepage    : getVendorHomePage(),
        vcsUrl   : getVendorSourceURL(),
        debianIteration: getDebianIteration(),
        rpmIteration: getRPMIteration(),
        license     : "GPL-2.0+CE",
        maintainer  : getVendor(),
        vendor      : getVendor(),
        packageName : "${getPackageName()}-${jdkMajorVersion}-${getJdkVirtualMachine().packageQualifier}${jdkDistributionType.pkgNameSuffix}"
    ]
    artifactory = [
        url       : "https://adoptopenjdk.jfrog.io/adoptopenjdk/",
        user      : project.hasProperty("ARTIFACTORY_USER") ? ARTIFACTORY_USER : null,
        password  : project.hasProperty("ARTIFACTORY_PASSWORD") ? ARTIFACTORY_PASSWORD : null,
        repository: [
            deb: project.hasProperty("ARTIFACTORY_REPOSITORY_DEB") ? ARTIFACTORY_REPOSITORY_DEB : null,
            rpm: project.hasProperty("ARTIFACTORY_REPOSITORY_RPM") ? ARTIFACTORY_REPOSITORY_RPM : null,
        ],
    ]
    jdkDistributionDir = JDK_DISTRIBUTION_DIR
    jdkDistributionType = getJdkDistributionType()
}

tasks.register("upload") {
    group = "upload"
    description = "Uploads all linux packages"
}

def getDebianIteration () {
    return hasProperty("DEBIAN_ITERATION") ? Integer.parseInt(DEBIAN_ITERATION) : 2
}

def getRPMIteration () {
    return hasProperty("RPM_ITERATION") ? Integer.parseInt(RPM_ITERATION) : 1
}

def getJdkDistributionType() {
    return hasProperty("JDK_DISTRIBUTION_TYPE") ?
        DistributionType.valueOf(JDK_DISTRIBUTION_TYPE.toUpperCase(Locale.US)) :
        DistributionType.JDK
}

def getJdkVirtualMachine() {
    return VirtualMachine.valueOf(JDK_VM.toUpperCase(Locale.US))
}

def getJdkArchitecture() {
    return hasProperty("JDK_ARCHITECTURE") ?
        Architecture.valueOf(JDK_ARCHITECTURE.toUpperCase(Locale.US)) :
        Architecture.X64
}

def getVendor() {
    return hasProperty("VENDOR") ? VENDOR : "AdoptOpenJDK"
}

def getPackageName() {
    return hasProperty("PACKAGE_NAME") ? PACKAGE_NAME : "adoptopenjdk"
}

def getVendorHomePage() {
    return hasProperty("VENDOR_HOMEPAGE") ? VENDOR_HOMEPAGE : "https://adoptopenjdk.net/"
}

def getVendorSourceURL() {
    return hasProperty("VENDOR_SOURCE_URL") ? VENDOR_SOURCE_URL : "https://github.com/AdoptOpenJDK/openjdk-jdk${jdkMajorVersion}u"
}

def getJdkMajorVersion() {
    def versionString = hasProperty("JDK_MAJOR_VERSION") ? JDK_MAJOR_VERSION : ""
    if (!versionString.matches("^[0-9]+\$")) {
        throw new IllegalArgumentException("Unparsable JDK major version '$versionString'")
    }
    int version = Integer.valueOf(versionString)

    // Do not change the numbers without updating the metadata declarations
    // (dependencies, jinfo priorities)!
    if (version < 8 || version > 16) {
        throw new IllegalArgumentException("Building packages for JDK $version is not supported")
    }
    return version
}

enum DistributionType {
    JDK("", "OpenJDK Development Kit"),
    JRE("jre", "OpenJDK Runtime Environment")

    private final String suffix

    private final String description

    DistributionType(suffix, description) {
        this.suffix = suffix
        this.description = description
    }

    String getSuffix() {
        return suffix
    }

    String getPkgNameSuffix() {
        if (suffix.empty) {
            return ""
        }

        return "-${this.suffix}"
    }

    String getDescription() {
        return description
    }
}
